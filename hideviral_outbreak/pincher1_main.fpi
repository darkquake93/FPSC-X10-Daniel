desc          = Random Melee Attack 

;Triggers

;MONITOR CHARACTER'S HEALTH, STARTS AT 300, IF LESS THAN 200 GO INTO RAGDOLL AND USE SHADER VARIABLE TO TURN OFF EYES
:healthless=200:suspend,coloff,ragdoll,sound=audiobank\viral_outbreak\pincher_die.wav,selectshadervariable=2,setshadervariable=0



;STAND IDLE AND DETECT PLAYER WHEN CLOSE, SET SHADER VARIABLE 1 TO USE RED EYES
;SHADER VARIABLE 2 CONTROLS GLOBAL EYE ILLUMINATION, TURN IT ON HERE, TURN IT OFF WHEN DEAD
:state=0,plrdistfurther=250:sethealth=300,animate=92,selectshadervariable=1,setshadervariable=0
:state=0:selectshadervariable=2,setshadervariable=100
;MAKE INVINCIBLE WHILE OUT OF ATTACK RANGE SO PLAYER CAN'T TAKE CHEAP SHOTS
:state=0,plrdistfurther=250,shotdamage=1:sethealth=300,animate=92
:state=0,plrdistwithin=250,plrcanbeseen:state=5,rotatetoplr,sound=audiobank\viral_outbreak\jaws_intro.wav



;CHOOSE MELEE ATTACK ROUTINE
:state=5,random=6:state=1
:state=5,random=6:state=6
:state=5,random=6:state=12



;MELEE ATTACK 1 (DOUBLE SLASH ATTACK/WALKING)
:state=1,plrdistfurther=65:rotatetoplr,movefore=10,animate=93
:state=1,plrdistwithin=65:rotatetoplr,setframe=95,state=7

:state=7,framebeyond=95 23,plrdistwithin=70:plraddhealth=-5,sound=audiobank\viral_outbreak\jaws_bite.wav,state=8
:state=7:incframe=95,rotatetoplr
:state=7,frameatend=95:state=5,sound=audiobank\viral_outbreak\jaws_intro.wav

:state=8,framebeyond=95 75,plrdistwithin=70:plraddhealth=-2,sound=audiobank\misc\melee.wav,state=9
:state=8:incframe=95
:state=8,frameatend=95:state=5

:state=9:incframe=95
:state=9,frameatend=95:state=5



;MELEE ATTACK 2 (SINGLE SLASH/RUNNING)
:state=6,plrdistfurther=65:rotatetoplr,runfore=10,animate=94
:state=6,plrdistwithin=65:rotatetoplr,setframe=96,state=2

:state=2,framebeyond=96 60,plrdistwithin=70:plraddhealth=-5,sound=audiobank\viral_outbreak\jaws_bite.wav,state=3
:state=2:incframe=96,rotatetoplr
:state=2,frameatend=96:state=5,sound=audiobank\viral_outbreak\jaws_intro.wav

:state=3:incframe=96
:state=3,frameatend=96:state=5


;VULNERABILITY RAGE ANIMATION/WALK CYCLE
:state=12,plrdistfurther=65:rotatetoplr,runfore=10,animate=94
:state=12,plrdistwithin=65:rotatetoplr,setframe=97,state=78,sethealth=300

;START INCREASING SHADER VARIABLE TO MAKE YELLOW EYES, SET HEALTH TO 1 POINT ABOVE DEATH SO ANY GUNSHOT DURING THIS TIME WILL KILL CHARACTER
:state=78,framewithin=97 50:selectshadervariable=1,incshadervariable=1
:state=78,shadervariablegreater=10:sethealth=201

:state=78,framebeyond=97 20,plrdistwithin=160:sound=audiobank\viral_outbreak\pincher_rage.wav,state=89
:state=78:incframe=97,rotatetoplr
:state=78,frameatend=97:state=5,sound=audiobank\viral_outbreak\jaws_intro.wav

;START DECREASING SHADER VARIABLE BACK DOWN TO NORMAL RED EYES, PLAYER STILL HAS A CHANCE TO KILL CHARACTER BEFORE RETURNING TO NORMAL
;ONCE BACK TO NORMAL, IF CHARACTER HAS NOT BEEN KILLED, RESET HEALTH TO STARTING VALUE
:state=89:incframe=97
:state=89,shadervariablegreater=0,framebeyond=97 80:selectshadervariable=1,decshadervariable=1
:state=89,shadervariablegreater=10:sethealth=201
:state=89,frameatend=97:state=5,sound=audiobank\viral_outbreak\jaws_intro.wav,selectshadervariable=1,setshadervariable=0,sethealth=300










